#!/usr/bin/env perl
use strict;
use warnings;

#-------------------------------------------------------------------
# libraries

use Data::Dumper;
use Getopt::Long;
use FindBin;
use lib "$FindBin::RealBin/../perl5";
use MDU;
use MDU::Logger qw(msg err);
use Cwd qw(abs_path);

#-------------------------------------------------------------------
# templates for outputs

my %template = (
  'R1'		=> "%1",
  'R2'		=> "%2",
  'R12'		=> "%1 %2",
  'nullarbor'   => "%i\t%1\t%2\n",
  'snippy'      => "--R1 %1 --R2 %2",
  'bwamem'      => "%1 %2",
  'megahit'	=> "--presets bulk --min-contig-len 500 -1 %1 -2 %2",
  'velvet'      => "-shortPaired -fmtAuto -separate %1 %2",
  'spades'      => "-1 %1 -2 %2",
  'bowtie'      => "-1 %1 -2 %2",
  'pear'        => "-f %1 -r %2",
  'kraken'      => "kraken --preload --paired %1 %2 | kraken-report > %i.kraken.tab\n",
  'symlink'     => "ln -s %1 %i_R1.fq.gz ; ln -s %2 %i_R2.fq.gz\n",
  'dirlink'     => "mkdir %i && ln -s %1 %i/%i_R1.fq.gz && ln -s %2 %i/%i_R2.fq.gz\n",
);

sub list_formats {
  for my $fmt (sort keys %template) { 
    my $desc = $template{$fmt};
    $desc =~ s/\t/ <tab> /g;
    $desc =~ s/\n/ <newline> /g;
    my $fmt = sprintf "%-10s", $fmt;
    msg("$fmt\t$desc");
  }
  exit;
}

#-------------------------------------------------------------------
# command line

my $verbose = 0;
my $idfile = '';
my $quiet   = 0;
my $format  = 'nullarbor';
my $rootdir = $MDU::MDUDIR;
my $skip = 0;

sub usage {
  my $EXE = $FindBin::RealScript;
  my @fmt = sort keys %template;
  print <<"USAGE";
Synopsis:
  Retrieve path to read files of MDU sequences isolates
Usage: 
  $EXE [options] [--format XXX] <ID1> [<ID2> ...]
  $EXE [options] [--format XXX] --idfile <file_of_IDs.txt>
Options:
  --help	This help
  --verbose	Extra debugging output
  --quiet	No screen output
  --list        Describe the supported formats
  --format	Output style: @fmt (DEFAULT=$format)
  --rootdir     Folder where reads are contained (DEFAULT=$rootdir)
  --idfile      File of IDs to use
  --skip        Skip over missing isolates, don't quit with error
USAGE
  exit;
}

GetOptions(
  "help"     => \&usage,
  "verbose"  => \$verbose,
  "quiet"    => \$quiet,
  "format=s" => \$format,
  "idfile=s" => \$idfile,
  "rootdir=s" => \$rootdir,
  "list"     => \&list_formats,
  "skip"     => \$skip,
) 
or usage();

MDU::Logger->quiet($quiet);

exists $template{$format} or err("Invalid --format '$format'");

#-------------------------------------------------------------------
# main script

$MDU::MDUDIR = abs_path( $rootdir );

my @ID;

if ($idfile) {
  open IDFILE, '<', $idfile or err("Could not read --idfile $idfile");
  @ID = <IDFILE>; # slurp all lines
  chomp @ID;
}
else {
  @ID = @ARGV;
}

@ID or err("No isolate IDs provided.");
my $fmt = $template{$format};
if ($fmt !~ m/\n$/ and @ID > 1) {
  err("--format $format is only suitable for one ID at a time");
}

my %seen;

my $blurt = $skip ? \&msg : \&err;

for my $name (@ID) {
  my $id = MDU->id($name) or do { $blurt->("Invalid ID: $name"); next };
  my @reads = MDU->reads($id) or do { $blurt->("No/multiple paired reads: $name"); next };
  $seen{$id}++ and do { $blurt->("Duplicate ID: $id (from $name)"); next };
  my $line = $fmt;
  $line =~ s/%i/$id/;
#  $line =~ s/%1/\Q$reads[0]\E/;
#  $line =~ s/%2/\Q$reads[1]\E/;
  $line =~ s/%1/$reads[0]/g;
  $line =~ s/%2/$reads[1]/g;
  $line =~ s/%i/$id/g;
  print "$line";
}

#-------------------------------------------------------------------



