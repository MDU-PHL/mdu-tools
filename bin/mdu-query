#!/usr/bin/env perl
use strict;
use warnings;

#-------------------------------------------------------------------
# libraries

use Data::Dumper;
use Getopt::Long;
use FindBin;
use lib "$FindBin::RealBin/../perl5";
use MDU;
use MDU::Logger qw(msg err);
use List::Util qw(sum);

#-------------------------------------------------------------------
# command line

my $verbose = 0;
my $db_file = $MDU::MDUDIR."/../QC/mdu-wgs.csv";
my $logical_or = 0;
my $tab = 0;
my($species, $scheme, $seqtype, $isolate, $date);

sub usage {
  my $EXE = $FindBin::RealScript;
  print <<"USAGE";
Synopsis:
  Query the MDU WGS database
Usage: 
  $EXE [options] 
Options:
  --help	This help
  --verbose	Extra debugging output ($verbose)
  --tab	        Write TSV instead of CSV
  --db FILE     CSV of isolate data ($db_file)
  --or          Do logical OR instead of AND
  --species P   Match SPECIES 
  --st      X   Match ST
  --scheme  P   Match SCHEME
  --date    P   Match ISOLATE
  --id      P   Match ANALYSIS_DATE
USAGE
  exit;
}

GetOptions(
  "help"      => \&usage,
  "verbose"   => \$verbose,
  "db=s"      => \$db_file,
  "tab"       => \$tab,
  "or"        => \$logical_or,
  "species=s" => \$species,
  "st=s"      => \$seqtype,
  "scheme=s"  => \$scheme,
  "id=s"      => \$isolate,
  "date=s"    => \$date,
) 
or usage();

#-------------------------------------------------------------------
# main script

my %db;
msg("Opening database: $db_file");
open CSV, '<', $db_file;
my $header = <CSV>;
my(@hdr) = split m/,/, $header;
chomp @hdr;
while (my $line = <CSV>) {
  chomp $line;
  my @x = split m/,/, $line;
  for my $i (0 .. $#x) {
    $db { $x[0] } { $hdr[$i] } = $x[$i];
  }
  $line =~ s/,/\t/g if $tab;
  $db{$x[0]}{LINE} = $line;
}
close CSV;
msg("Read", scalar(keys %db), "isolates.");

my $pass = sum( map { $_ ? 1 : 0 } ($species, $seqtype, $scheme, $isolate, $date) );
$pass = 1 if $logical_or and $pass >= 1;
$pass ||= 1E9;
my @match;
for my $id (keys %db) {
  my $score=0;
  $score++ if $species and $db{$id}{SPECIES} =~ m/$species/i;
  $score++ if $scheme  and $db{$id}{SCHEME}  =~ m/$scheme/i;
  $score++ if $seqtype and $db{$id}{ST}      eq $seqtype;
  $score++ if $isolate and $db{$id}{ISOLATE} =~ m/$isolate/i;
  $score++ if $date    and $db{$id}{ANALYSIS_DATE} =~ m/$date/;
  push @match, $db{$id}{LINE} if $score >= $pass;
}
msg("Found", 0+@match, "matches");
print $header, map { "$_\n" } @match if @match > 0;

#-------------------------------------------------------------------






